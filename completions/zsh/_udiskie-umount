#compdef udiskie-umount
# vim: ft=zsh sts=2 sw=2 ts=2

function _udiskie-umount
{
  local context curcontext="$curcontext" line state ret=1
  typeset -A opt_args

  args=(
    '(- *)'{-h,--help}"[show help]"
    '(- *)'{-V,--version}"[show version]"
    '(-q)'{-v,--verbose}"[more output]"
    '(-v)'{-q,--quiet}"[less output]"
    '(-C)'{-c,--config}"[set config file]:file:_files"
    '(-c)'{-C,--no-config}"[don't use config file]"
    '(*)'{-a,--all}"[unmount all devices]"
    '(-D)'{-d,--detach}"[detach device]"
    '(-d)'{-D,--no-detach}"[don't detach device]"
    '(-E)'{-e,--eject}"[eject device]"
    '(-e)'{-E,--no-eject}"[don't eject device]"
    '(-F)'{-f,--force}"[recursive unmounting]"
    '(-f)'{-F,--no-force}"[no recursive unmountinng]"
    '(-L)'{-l,--lock}"[lock device after unmounting]"
    '(-l)'{-L,--no-lock}"[don't lock device]"
    '*:dev or dir:->udevordir'
  )
  _arguments -C -s "$args[@]" && ret=0

  case "$state" in
  udevordir)
    local dev_tmp mp_tmp loop_tmp dev_detail

    # "${(@f)X}" means to use lines as separators
    dev_detail=( "${(@f)$(udiskie-info -a -o '{device_presentation}<:1:>{mount_path}<:2:>{is_filesystem}<:3:>{is_mounted}<:4:>{is_loop}<:5:>{loop_file}')}" )

    # select: 'device_presentation'
    dev_tmp=( ${dev_detail%%<:1:>*} )

    # filter: 'is_filesystem' and 'is_mounted'
    mp_tmp=( ${(M)dev_detail:#*<:2:>True<:3:>True<:4:>*} )
    # select: 'mount_path'
    mp_tmp=( ${mp_tmp##*<:1:>} )
    mp_tmp=( ${mp_tmp%%<:2:>*} )

    # filter: 'is_loop'
    loop_tmp=( ${(M)dev_detail:#*<:3:>True<:5:>*} )
    # select: 'mount_path'
    loop_tmp=( ${loop_tmp##*<:5:>} )

    _alternative \
      'directories:mount point:_udiskie-canonical_paths -A mp_tmp -N directories mount\ point' \
      'device-paths: device path:_udiskie-canonical_paths -A dev_tmp -N device-paths device\ path' \
      'loop-files: loop file:_udiskie-canonical_paths -A loop_tmp -N loop-files loop\ file' \
      && ret=0

  ;;
  esac
  return ret
}

_udiskie-umount "$@"
